/*
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'

def gitVersion() {
    def process = "git rev-list master --first-parent --count".execute()
    return process.text.toInteger()
}

android {
    compileSdkVersion 22
    buildToolsVersion '22.0.1'

    def versionPropsFile = file('version.properties')

    if (versionPropsFile.canRead()) {
        def Properties versionProps = new Properties()

        versionProps.load(new FileInputStream(versionPropsFile))

        def majorVersion = versionProps['MAJOR_VERSION'].toInteger()
        def minorVersion = versionProps['MINOR_VERSION'].toInteger()
        def pointVersion = gitVersion()

        versionProps['POINT_VERSION'] = pointVersion.toString()
        versionProps.store(versionPropsFile.newWriter(), null)

        def name = "${majorVersion}.${minorVersion}.${pointVersion}"
        def code = gitVersion()
        println "version name: " + name
        println "version code: " + code

        defaultConfig {
            applicationId "my.home.lehome"
            minSdkVersion 17
            targetSdkVersion 22
            versionCode code
            versionName name

            ndk {
                //设置支持的SO库架构
                abiFilters 'armeabi' //, 'x86', 'armeabi-v7a', 'x86_64', 'arm64-v8a'
            }
        }
    } else {
        throw new GradleException("Could not read version.properties!")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

}

dependencies {
    compile project(':common')
    compile project(':domain')
    compile project(':model')
    compile files('libs/VoiceRecognition.jar')
    compile files('libs/jg_filter_sdk_1.1.jar')
    compile files('libs/BaiduLBS_Android.jar')
    compile('com.doomonafireball.betterpickers:library:1.5.3') {
        exclude group: 'com.android.support', module: 'support-v4'
    }
    compile files('libs/wup-1.0.0.E-SNAPSHOT.jar')
    compile files('libs/Xg_sdk_v2.40_20150527_1022.jar')
    compile 'com.android.support:design:22.2.1'
    compile 'com.android.support:support-v4:22.2.1'
    compile 'com.akexorcist:RoundCornerProgressBar:1.0.0'
    compile 'com.skyfishjy.ripplebackground:library:1.0.1'
    compile 'org.zeromq:jeromq:0.3.4'
    compile 'com.android.support:recyclerview-v7:22.2.1'
    compile 'org.altbeacon:android-beacon-library:2.+'
    compile 'com.nostra13.universalimageloader:universal-image-loader:1.9.3'
    compile 'com.davemorrissey.labs:subsampling-scale-image-view:3.1.3'
    compile 'com.facebook.network.connectionclass:connectionclass:1.0.1'
    compile 'com.mcxiaoke.volley:library:1.0.19'
    compile 'com.tencent.bugly:crashreport:1.2.9'
    compile files('libs/MiPush_SDK_Client_2_2_21.jar')
}
